apply plugin: 'com.android.application'

dependencies {
    compileOnly "com.google.code.findbugs:jsr305:${JSR_305_VERSION}"
    implementation "com.github.bumptech.glide:glide:${GLIDE_VERSION}"
    annotationProcessor "com.github.bumptech.glide:compiler:${GLIDE_VERSION}"
    implementation "com.android.volley:volley:${VOLLEY_VERSION}"
    implementation "com.nostra13.universalimageloader:universal-image-loader:${UIL_VERSION}"
    implementation "com.squareup.picasso:picasso:${PICASSO_VERSION}"
    implementation "com.jakewharton.picasso:picasso2-okhttp3-downloader:1.0.2"
    implementation "com.android.support:appcompat-v7:${SUPPORT_LIB_VERSION}"
    implementation "com.android.support:recyclerview-v7:${SUPPORT_LIB_VERSION}"
    implementation "com.googlecode.android-query:android-query:${AQUERY_VERSION}"
    implementation "com.facebook.stetho:stetho-okhttp3:${STETHO_VERSION}"

    implementation project(':drawee')
    implementation project(':drawee-backends:drawee-pipeline')
    implementation project(':drawee-backends:drawee-volley')
    implementation project(':fbcore')
    implementation project(':imagepipeline')
    implementation project(':imagepipeline-backends:imagepipeline-okhttp3')
    implementation project(':animated-gif')
    implementation project(':tools:stetho')
}

android {
    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdkVersion rootProject.ext.compileSdkVersion

    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId "com.facebook.samples.comparison"
        minSdkVersion rootProject.ext.samplesMinSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "${VERSION_NAME}"

        testApplicationId "com.facebook.samples.comparison.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.pro'
        }
    }

    lintOptions {
        warning 'InvalidPackage'
        abortOnError false
    }

    splits {
        abi {
            enable true
            reset()
            include 'x86', 'x86_64', 'arm64-v8a', 'armeabi-v7a'
            universalApk false
        }
    }
}
