/*
 * Copyright (c) Facebook, Inc. and its affiliates.
 *
 * This source code is licensed under the MIT license found in the
 * LICENSE file in the root directory of this source tree.
 */

import com.facebook.fresco.buildsrc.Deps

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    buildToolsVersion FrescoConfig.buildToolsVersion
    compileSdkVersion FrescoConfig.compileSdkVersion

    defaultConfig {
        applicationId "com.facebook.fresco.samples.kotlin"
        minSdkVersion FrescoConfig.samplesMinSdkVersion
        targetSdkVersion FrescoConfig.targetSdkVersion
        versionCode 1
        versionName "${VERSION_NAME}"

        testApplicationId "com.facebook.fresco.samples.kotlin.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true // needed for tinting
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        warning 'InvalidPackage'
        abortOnError false
    }

    splits {
        abi {
            enable true
            reset()
            include 'x86', 'x86_64', 'arm64-v8a', 'armeabi-v7a'
            universalApk false
        }
    }
}

dependencies {
    compileOnly Deps.jsr305
    implementation Deps.Kotlin.stdlibJdk
    implementation "androidx.appcompat:appcompat:${versions.androidx.appcompat}"
    implementation "com.google.android.material:material:${versions.material}"
    implementation "androidx.recyclerview:recyclerview:${versions.androidx.recyclerview}"
    implementation "androidx.cardview:cardview:${versions.androidx.cardview}"
    implementation Deps.Tools.Stetho.okhttp3

    implementation project(':drawee-backends:drawee-pipeline')
    implementation project(':drawee-span')
    implementation project(':imagepipeline-backends:imagepipeline-okhttp3')
    implementation project(':tools:stetho')
    implementation project(path: ':imagepipeline-backends:imagepipeline-okhttp3')
}
