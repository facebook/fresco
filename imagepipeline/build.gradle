apply plugin: 'com.android.library'
apply plugin: 'maven'

project.group = GROUP
version = VERSION_NAME

dependencies {
    compileOnly "androidx.annotation:annotation:${versions.androidx.annotation}"
    compileOnly "androidx.legacy:legacy-support-core-utils:${versions.androidx.legacy_support_core_utils}"
    compileOnly "com.google.code.findbugs:jsr305:${JSR_305_VERSION}"
    compileOnly "com.facebook.infer.annotation:infer-annotation:${INFER_ANNOTATION_VERSION}"
    compileOnly "javax.annotation:javax.annotation-api:${ANNOTATION_API_VERSION}"

    api project(':imagepipeline-base')
    implementation "com.facebook.soloader:soloader:${SOLOADER_VERSION}"

    implementation "com.parse.bolts:bolts-tasks:${BOLTS_ANDROID_VERSION}"
    implementation project(':fbcore')

    testImplementation "androidx.legacy:legacy-support-core-utils:${versions.androidx.legacy_support_core_utils}"
    testImplementation "com.google.code.findbugs:jsr305:${JSR_305_VERSION}"
    testImplementation project(':imagepipeline-test')
    testImplementation project(':imagepipeline-base-test')
    testImplementation project(':native-imagetranscoder')
    testImplementation "junit:junit:${JUNIT_VERSION}"
    testImplementation "org.easytesting:fest-assert-core:${FEST_ASSERT_CORE_VERSION}"
    testImplementation "org.mockito:mockito-core:${MOCKITO_CORE_VERSION}"
    testImplementation("org.powermock:powermock-api-mockito:${POWERMOCK_VERSION}") {
        exclude group: 'org.mockito', module: 'mockito-all'
    }
    testImplementation "org.powermock:powermock-module-junit4:${POWERMOCK_VERSION}"
    testImplementation "org.powermock:powermock-module-junit4-rule:${POWERMOCK_VERSION}"
    testImplementation "org.powermock:powermock-classloading-xstream:${POWERMOCK_VERSION}"
    testImplementation("org.robolectric:robolectric:${ROBOLECTRIC_VERSION}") {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
}
apply from: rootProject.file('release.gradle')

android {
    def ndkLibs = [
        ['bitmaps', []],
        ['imagepipeline', []],
        ['memchunk', []]]

    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
    }

    sourceSets {
        main {
            jni.srcDirs = []
            jniLibs.srcDirs = ndkLibs.collect { "$buildDir/${it[0]}" }
        }
        test {
            java {
                exclude '**/LocalVideoThumbnailProducerTest.java'
                exclude '**/DecodeProducerTest.java'
                exclude '**/ProgressiveJpegParserTest.java'
                exclude '**/AnimatedImageFactoryTest.java'

            }
            resources {
                exclude '**/BUCK'
            }
        }
    }
    ndkLibs.each { lib -> makeNdkTasks lib[0], lib[1] }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
artifacts.add('archives', sourcesJar)
