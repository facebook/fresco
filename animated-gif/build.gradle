/*
 * Copyright (c) Meta Platforms, Inc. and affiliates.
 *
 * This source code is licensed under the MIT license found in the
 * LICENSE file in the root directory of this source tree.
 */

import com.facebook.fresco.buildsrc.Deps
import com.facebook.fresco.buildsrc.GradleDeps
import com.facebook.fresco.buildsrc.TestDeps

apply plugin: 'com.android.library'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

dependencies {
    compileOnly Deps.inferAnnotation
    compileOnly Deps.jsr305
    implementation Deps.Bolts.tasks
    compileOnly Deps.javaxAnnotation
    implementation Deps.SoLoader.nativeloader
    implementation project(':fbcore')
    implementation project(':animated-base')

    testCompileOnly Deps.inferAnnotation
    testImplementation project(':imagepipeline-base-test')
    testImplementation project(':imagepipeline-test')
    testImplementation project(':middleware')
    testImplementation TestDeps.junit
    testImplementation TestDeps.mockitoCore
    testImplementation(TestDeps.Powermock.apiMockito) {
        exclude group: 'org.mockito', module: 'mockito-all'
    }
    testImplementation TestDeps.Powermock.moduleJunit4Rule
    testImplementation TestDeps.Powermock.classloadingXstream
    testImplementation(TestDeps.robolectric) {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
}

// We download various C++ open-source dependencies from SourceForge into nativedeps/downloads.
// We then copy both downloaded code and our custom makefiles and headers into nativedeps/merge.

task fetchNativeDeps(dependsOn: [copyGiflib]) {
}

android {
    ndkVersion GradleDeps.Native.version

    def ndkLibs = [['gifimage', [copyGiflib]]]

    buildToolsVersion FrescoConfig.buildToolsVersion
    compileSdkVersion FrescoConfig.compileSdkVersion

    namespace "com.facebook.animated.gif"

    defaultConfig {
        minSdkVersion FrescoConfig.minSdkVersion
        targetSdkVersion FrescoConfig.targetSdkVersion
    }

    sourceSets {
        main {
            jni.srcDirs = []
            jniLibs.srcDirs = ndkLibs.collect { "$buildDir/${it[0]}" }
        }
    }
    ndkLibs.each { lib -> makeNdkTasks lib[0], lib[1] }
    preBuild.dependsOn("ndk_build_gifimage")
}

apply plugin: "com.vanniktech.maven.publish"
